# Maintainer: Wachid Adi Nugroho <wachidadinugroho.maya@gmail.com>

_pkgname=mauiman
pkgname=$_pkgname-git
pkgver=3.1.0.r5.gd4f86c6
pkgrel=2
pkgdesc='Maui Manager Library. Server and public library API.'
url='https://invent.kde.org/maui/mauiman'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3)
depends=(qt6-base)
makedepends=(git extra-cmake-modules)
groups=(maui)
#provides=($_pkgname) #Arch repo is still Qt5
conflicts=($_pkgname)
source=(git+$url.git)
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build -S "$_pkgname" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
