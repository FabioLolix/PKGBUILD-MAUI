# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: KaMeHb-UA

pkgname=maui-bonsai-git
pkgver=1.1.3.r11.g7cdda61
pkgrel=1
pkgdesc="Mobile Git repository manager"
arch=(x86_64 i686 armv7h aarch64)
url="https://invent.kde.org/maui/bonsai"
license=(GPL3)
depends=(
  ki18n
  knotifications
  kio
  attica
  mauikit-git
  mauikit-filebrowsing-git
  kirigami2
  qt5-svg
  qt5-quickcontrols2
  qt5-multimedia
  libgit2
)
makedepends=(git 'extra-cmake-modules')
provides=('maui-bonsai')
conflicts=('maui-bonsai')
source=("git+https://invent.kde.org/maui/bonsai.git#branch=qt6")
sha256sums=('SKIP')


pkgver() {
  cd "bonsai"
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build -S "bonsai" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
