# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: TheCynicalLiger
# Contributor: Lerentis <lerentis@burntbunch.org>

pkgname=index-fm-git
pkgver=3.1.0.r69.g0d35b42
pkgrel=1
pkgdesc="Index is a file manager that works on desktops, Android and Plasma Mobile"
arch=(x86_64 i686 armv7h aarch64)
url="https://invent.kde.org/maui/index-fm"
license=(BSD-2-Clause CC0-1.0 LGPL-3.0)
depends=(mauikit-git mauikit-filebrowsing-git qmltermwidget)
makedepends=(git cmake extra-cmake-modules)
provides=(index-fm)
conflicts=(index-fm)
source=("git+https://invent.kde.org/maui/index-fm.git")
sha256sums=('SKIP')

pkgver() {
  cd "index-fm"
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build -S "index-fm" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
