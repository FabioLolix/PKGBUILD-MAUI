# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Wachid Adi Nugroho <wachidadinugroho.maya@gmail.com>

pkgname=maui-shell-git
pkgver=0.6.6.r17.geefee15
pkgrel=1
pkgdesc="Maui Shell is a convergent shell for desktops, tablets, and phones"
arch=(x86_64 i686 armv7h aarch64)
url="https://github.com/Nitrux/maui-shell"
license=(LGPL-3.0-only)
depends=(bluedevil
         cask-server-git
         fontconfig
         karchive
         kconfig
         kcoreaddons
         kcrash
         kdbusaddons
         kdeclarative
         kded
         kdesu
         # kdoctools
         kglobalaccel
         ki18n
         kiconthemes
         kidletime
         kinit
         kio
         kitemmodels
         knotifications
         knotifyconfig
         kpackage
         kpeople
         krunner
         kservice
         ktexteditor
         kunitconversion
         kwallet
         kwayland
         maui-core-git
         mauikit-calendar-git
         mauikit-filebrowsing-git
         mauikit-git
         mauiman-git
         phonon-qt6
         plasma-nm
         plasma-pa
         polkit-qt6
         prison
         qt6-base
         qt6-declarative
         qt6-svg
         qt6-wayland
         solid)
makedepends=(git extra-cmake-modules)
optdepends=('maui-settings-git: Settings Manager Apps for the Maui DE')
groups=(maui)
provides=(maui-shell)
conflicts=(maui-shell)
source=("git+https://github.com/Nitrux/maui-shell.git")
sha256sums=('SKIP')

pkgver() {
  cd maui-shell
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build -S "maui-shell" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
